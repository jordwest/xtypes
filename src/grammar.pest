alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
underscore = @{ "_" }
ident = @{ alpha ~ (alpha | digit | underscore)* }
dotted_ident = @{ alpha ~ (alpha | digit | underscore | ".")* }
WHITESPACE = _{ (" " | "\t" | "\r" | "\n") }
// COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* ~ "\n" }
string_value = { (!("\"") ~ ANY)* }


attribute = {"#[" ~ dotted_ident ~ "=" ~ "\"" ~ string_value ~ "\"" ~ "]"}
variant = { attribute* ~ ident }
enum_message = { "|"? ~ variant ~ ("|" ~ variant)* }

optional = { "?" }
field_name = { ident }
field_type = { ident }
struct_field = { attribute* ~ field_name ~ optional? ~ ":" ~ field_type ~ "," }
struct_message = { "{" ~ struct_field* ~ "}" }

message_keyword = _{ "message" }
message = { message_keyword ~ ident ~ "=" ~ (enum_message | struct_message) ~ ";"}

module_decl = { attribute* ~ "module" ~ dotted_ident ~ ";" }

file = { WHITESPACE* ~ module_decl ~ message* ~ EOI }
